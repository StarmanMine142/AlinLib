apply plugin: 'maven-publish'
apply plugin: 'io.github.goooler.shadow'
apply plugin: 'dev.architectury.loom'
apply plugin: 'com.replaymod.preprocess'
apply plugin: 'me.fallenbreath.yamlang'
apply plugin: "io.github.p03w.machete"

def mod_brand = (loom.platform.get() as String).toLowerCase()
assert mod_brand in ['fabric', 'forge', 'neoforge']
assert project.name.endsWith('-' + mod_brand)  // optional check
archivesBaseName = rootProject.archives_base_name + "-" + mod_brand

int mcVersion = 1
preprocess {
    mcVersion = vars.get()["MC"] as int
    tabIndentation = true

    vars.put("MC", mcVersion)
    vars.put("FABRIC", mod_brand == 'fabric' ? 1 : 0)
    vars.put("FORGE", mod_brand == 'forge' ? 1 : 0)
    vars.put("NEOFORGE", mod_brand == 'neoforge' ? 1 : 0)
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://jitpack.io' }
    maven { url = "https://maven.terraformersmc.com/releases" } // MODMENU
    maven { url = "https://maven.neoforged.net/releases/" } // NEOFORGE
    maven { url = "https://maven.meteordev.org/releases" } // STARSCRIPT
    maven { url = "https://maven.parchmentmc.org" } // MAPPINGS
}
configurations {
    modRuntimeOnly.exclude group: 'net.fabricmc', module: 'fabric-loader'
}

loom {
    runConfigs.client {
        // to make sure it generates all "Minecraft Client (:subproject_name)" applications
        ideConfigGenerated = true
        runDir '../../run'
        vmArgs '-Dmixin.debug.export=true'
    }

    accessWidenerPath = file("../../src/main/resources/alinlib.accesswidener")
    if (mod_brand == 'forge') {
        forge {
            convertAccessWideners = true
            mixinConfig "alinlib.mixin.json"
        }
    }
}

remapJar {
    remapperIsolation = true
}
machete {
    ignoredTasks.add("shadowJar")
    ignoredTasks.add("jar")
}

configurations {shadow}
dependencies {
    // loom
    implementation("com.google.code.gson:gson:2.10.1")
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
    }

    if (mod_brand == 'fabric') {
        modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
        modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    } else if (mod_brand == 'neoforge') {
        neoForge "net.neoforged:neoforge:${project.forge_version}"
    } else {
        forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

        include "io.github.llamalad7:mixinextras-forge:0.3.6"
    }
    include implementation("meteordevelopment:starscript:0.2.2")
}

int JAVA_COMPATIBILITY
if (mcVersion >= 12005) {
    JAVA_COMPATIBILITY = 21
} else {
    JAVA_COMPATIBILITY = 17
}
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = JAVA_COMPATIBILITY
}

version = project.mod_version + "+mc" + project.minecraft_version

processResources {
    inputs.property "version", version
    inputs.property "minecraft_dependency", project.minecraft_dependency

    [
            'fabric': ['fabric.mod.json'],
            'neoforge': ['META-INF/neoforge.mods.toml'],
            'forge': ['META-INF/mods.toml', 'pack.mcmeta'],
    ].forEach { brand, files ->
        files.forEach { name ->
            if (brand == mod_brand) {
                filesMatching(name) {
                    def valueMap = [
                            "version": version,
                            "minecraft_dependency": project.minecraft_dependency,
                    ]
                    expand valueMap
                }
            } else {
                exclude name
            }
        }
    }
}
